buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.+'
    }
}

apply plugin: 'com.android.application'

repositories {
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
}

android {
    compileSdkVersion 20
    buildToolsVersion "20"

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 20
    }

    productFlavors {
        production{
            applicationId "com.zion.htf"
        }

        analytics{
            applicationId "com.zion.htf.analytics"
        }
    }

    signingConfigs{
        release {
            storeFile file(System.getenv("HOME") + "/.android/release.keystore")
            storePassword "dummy"
            keyAlias "sbooob"
            keyPassword "dummy"
        }
    }

    buildTypes {
        release {
            zipAlign true
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        checkReleaseBuilds true
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    packagingOptions {
        exclude 'AndroidManifest.xml'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile files('libs/PayPalAndroidSDK.jar')
    compile 'com.google.android.gms:play-services:4.4.52'
    compile 'net.simonvt:android-numberpicker:1.0.0'
    compile 'com.mobsandgeeks.ui.library:TypefaceTextview:1.0.0@aar'
    compile 'se.emilsjolander:stickylistheaders:2.5.0'
    compile 'com.android.support:support-v4:20+'
    compile 'com.android.support:appcompat-v7:20+'
    compile 'com.soundcloud:java-api-wrapper:1.3.1'
    compile 'javax.servlet:javax.servlet-api:3.1.0'
}

task askForPasswords << {
    def storePwd = new String(System.console().readPassword("\n\$ Enter store password: "))
    def keyPwd = new String(System.console().readPassword("\n\$ Enter key password: "))

    android.signingConfigs.release.storePassword = storePwd
    android.signingConfigs.release.keyPassword = keyPwd
}


tasks.whenTaskAdded { theTask ->
    if (theTask.name.equals("packageRelease") || theTask.name.equals("packageProductionRelease") || theTask.name.equals("packageAnalyticsRelease")) {
        theTask.dependsOn("askForPasswords")
    }
}